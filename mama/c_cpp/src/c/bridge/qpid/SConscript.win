#!/usr/bin/python
# -*- coding: utf-8 -*-
import os
Import('*')
env = env.Clone()

target = 'libmamaqpidimpl'

env.Append( CPPDEFINES 	= ['BRIDGE', 'MAMA_DLL', 'HAVE_QPID_PROTON_VERSION_H'] )

libPath = []
libPath.append('$libdir')

incPath = []
incPath.append('$libevent_home/include')
incPath.append('$qpid_home/include')
incPath.append('#common/c_cpp/src/c/windows')
incPath.append('#common/c_cpp/src/c')
incPath.append('#mama/c_cpp/src/c')

libs = []
libs.append('libwombatcommon%s.lib' % ( env['suffix'] ))
libs.append('libmamac%s.lib' % ( env['suffix'] ))

if env['target_arch'] == 'x86_64':
  archPath = "x64"
else:
  archPath = ""

libs.append('event')


if 'debug' in env['build']:
  qpidLibName = "qpid-protond"
  buildType = "Debug"
else:
  qpidLibName = "qpid-proton"
  buildType = "Release"

libs.append(qpidLibName)

libPath.append('%s/lib' % env['qpid_home'])
libPath.append('%s/lib' % env['libevent_home'])

qpidInstLibs = '%s/bin/%s.dll' % (env['qpid_home'], qpidLibName)

env['CCFLAGS'].append(['/TP', '/WX-'])
env['CPPPATH'] = incPath
env.Append(LIBS = libs, LIBPATH=libPath) 

sources = Glob('*.c')

env.InstallLibrary(sources, target)

if 'dynamic' in env['build']:
    Alias ('install', env.Install('$bindir', qpidInstLibs))
